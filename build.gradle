plugins {
    id 'java'
    id 'idea'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}
tasks.withType(Test).configureEach {
    systemProperty("file.encoding", "UTF-8")
}
tasks.withType(Javadoc).configureEach {
    options.encoding = "UTF-8"
}

idea {
    module {
        inheritOutputDirs = true
        excludeDirs = [
                file('.gradle'),
                file('build/generated'),
                file('build/libs'),
                file('build/tmp'),
                file('build/classes'),  // remove to see compiled classes
                file('build'),          // remove to see compiled classes
        ]
    }
}

group 'com.github.andrebrait'
version '1.0.0-SNAPSHOT'


sourceSets.main {
    java.srcDirs = ['src/main/java']
}

sourceSets.test {
    java.srcDirs = ['src/test/java']
}

// https://docs.gradle.org/current/userguide/building_java_projects.html#sec:java_source_sets
sourceSets {
    jmh {
        java.srcDirs = ['src/jmh/java']
        resources.srcDirs = ['src/jmh/resources']
        compileClasspath += sourceSets.main.runtimeClasspath
        runtimeClasspath += sourceSets.test.runtimeClasspath
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.github.oshi:oshi-core:6.5.0'
    runtimeOnly 'org.slf4j:slf4j-simple:2.0.12'
    jmhImplementation 'org.openjdk.jmh:jmh-core:1.37'
    jmhAnnotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.37'
}

// to make sure benchmarks always get compiled
classes.finalizedBy(jmhClasses)